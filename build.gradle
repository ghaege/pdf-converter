plugins {
	id 'java'
	id 'idea'
    id 'org.springframework.boot' version '3.4.3'        // https://plugins.gradle.org/plugin/org.springframework.boot
                                                         // https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/
    id 'io.spring.dependency-management' version '1.1.7' // https://plugins.gradle.org/plugin/io.spring.dependency-management
}

// artifact
group = 'de.qaepps'
description = 'PdfConverter - Rest Api'
version = '1.0.7'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
	mavenCentral()
}

// fresh versions from 2025.03
ext {
    jodconverterVersion = '4.4.8'    // @see https://mvnrepository.com/artifact/org.jodconverter/jodconverter-local
    openApiVersion = '2.8.5'         // @see https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-starter-webmvc-ui
    commonsIoVersion = '2.18.0'      // @see https://mvnrepository.com/artifact/commons-io/commons-io
    jaxbVersion = '4.0.5'            // @see https://mvnrepository.com/artifact/org.glassfish.jaxb/jaxb-runtime
    testcontainersVersion = '1.20.6' // @see https://mvnrepository.com/artifact/org.testcontainers/testcontainers
    snakeyamlVersion = '2.4'         // @see https://mvnrepository.com/artifact/org.yaml/snakeyaml
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework:spring-core'

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$openApiVersion"

    implementation "org.jodconverter:jodconverter-local-lo:$jodconverterVersion"
    implementation "org.jodconverter:jodconverter-spring-boot-starter:$jodconverterVersion"

    implementation "commons-io:commons-io:$commonsIoVersion"
    implementation "org.glassfish.jaxb:jaxb-runtime:$jaxbVersion"
    implementation "org.yaml:snakeyaml:$snakeyamlVersion"

    testImplementation 'org.junit.platform:junit-platform-launcher'
    testImplementation "org.testcontainers:junit-jupiter:$testcontainersVersion"
    testImplementation "org.testcontainers:testcontainers:$testcontainersVersion"
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

jar {
	// disables generation of *.plain.jar
	enabled = false
}

bootJar {
	manifest {
		attributes(
				'Start-Class': 'de.qaepps.converter.PdfConverterApplication',
				'Built-By': System.properties['user.name'],
				'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
				'Implementation-Title': 'PdfConverter',
				'Implementation-Version': archiveVersion,
		)
	}
}

springBoot {
	mainClass = 'de.qaepps.converter.PdfConverterApplication'
}

task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/exploded")
}

// pure junit tests
test {
    useJUnitPlatform {
        includeTags 'mockTest'
    }
}

// integrationTest's
task integrationTest(type: Test) {
    useJUnitPlatform {
        includeTags 'integrationTest'
    }
}

// e2eTest's
task e2eTest(type: Test) {
    useJUnitPlatform {
        includeTags 'e2eTest'
    }
}
